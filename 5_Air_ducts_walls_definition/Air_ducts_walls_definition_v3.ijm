/*
 * The macro allows to define air ducts walls in the sample.
 * The macro requires files generated by the air duct identification tool. 
 * The present file must be copied to the "plugins" folder of ImageJ/Fiji.
 */

//Integration to ImageJ/Fiji menu
macro"Air_ducts_walls_definition"{

//Input files definition
//Opening of image
Dialog.create("Input file #1 (1/2)");
Dialog.addMessage("Open clean mask");
Dialog.show();
open();
Title = File.nameWithoutExtension;
rename(Title);
run("Select All");
//Opening of air ducts ROI set
Dialog.create("Input file #2 (2/2)");
Dialog.addMessage("Open air ducts ROI set");
Dialog.show();
open();

//Duplication of image
selectWindow(Title);
run("Duplicate...", "title=[Sample]");
close(Title);

//Definition of default analysis parameters
default_thick = 25;

//Air ducts walls thickness setting dialog
//Initialization
confirm = false;
auto = false;
current_thick = default_thick;
test = 0;

//Update
	while (confirm == false) {

		//Reset
			if (auto == true) {
				close("Sample_copy");
				close("Overlay_walls");
				close("Overlay_walls_copy");
				close("Overlay_air_ducts_walls");
				roiManager("Select", roiManager("Count") - 1);
				roiManager("Delete");
				current_thick = default_thick;
				test = 0;
			}

		//Deletion of intermediate files
			if (test != 0) {
				close("Sample_copy");
				close("Overlay_walls");
				close("Overlay_walls_copy");
				close("Overlay_air_ducts_walls");
				roiManager("Select", roiManager("Count") - 1);
				roiManager("Delete");
			}

		//Definition of air ducts walls
			selectWindow("Sample");
			roiManager("Select", roiManager("Count") - 1);
			run("Enlarge...", "enlarge=current_thick");
			roiManager("Add");
			roiManager("Select", roiManager("Count") - 1);
			roiManager("Rename", "Air_ducts_walls");
			roiManager("Set Fill Color", "green");

		//Visualization #1
			run("Select None");		
			selectWindow("Sample");
			run("Duplicate...", "title=[Sample_copy]");
			roiManager("Select", roiManager("Count") - 1);
			roiManager("Show All without labels");
			run("Flatten");
			rename("Overlay_walls");

		//Visualization #2
			run("Select None");		
			selectWindow("Overlay_walls");
			run("Duplicate...", "title=[Overlay_walls_copy]");
			roiManager("Select", roiManager("Count") - 2);
			run("Flatten");
			//Legend
				setColor("cyan");
				x = 0; y = 50;
				setFont("SansSerif", 50, "antiliased");
				drawString("Air ducts", x, y, "black");
				setColor("green");
				y += 50;
				setFont("SansSerif", 50, "antiliased");
				drawString("Air ducts walls", x, y, "black");
			rename("Overlay_air_ducts_walls");
		
		//Air ducts walls definition dialog
			Dialog.create("Definition of air ducts walls thickness");
			Dialog.addString("Applies to:", Title);
			Dialog.addString("test #:", test);
			Dialog.addNumber("Air ducts walls thickness (um):", current_thick);
			options = newArray("Test settings", "Confirm settings", "Reset (default: " + default_thick + " um)");
			Dialog.addRadioButtonGroup("Options:", options, 3, 1, "Test settings");
			Dialog.show();
		
		//Values update
			test = test + 1;
			current_thick = Dialog.getNumber();
			if (current_thick >= 0) {
				current_thick = current_thick;
			}
			if (current_thick < 0) {
				current_thick = - current_thick;
			}
			select = Dialog.getRadioButton();
			if (select == "Test settings") {
				confirm = false;
				auto = false;
			}
			if (select == "Confirm settings") {
				confirm = true;		
				auto = false;		
			}
			if (select == "Reset (default: " + default_thick + " um)") {
				confirm = false;
				auto = true;
			}
	
	}

//Ouput
	//Save dialog
	Dialog.create("Save output files");
	Dialog.addCheckbox("Save?", false);
	Dialog.show();
	
	save_output = Dialog.getCheckbox();
		if (save_output == true) {

			//Overlay #1
			selectWindow("Overlay_walls");
			saveAs("Tiff");
			//Overlay #2
			selectWindow("Overlay_air_ducts_walls");
			saveAs("Tiff", File.getDefaultDir + "Overlay_air_ducts_walls.tif");
			//ROI
			selectWindow("ROI Manager");
			roiManager("save", File.getDefaultDir + "ROI_set_walls.zip");
			run("Close");
			//Settings
			print("Analysis of: " + Title);
			print("Air ducts walls thickness: " + current_thick+" um");
			selectWindow("Log");
			saveAs("Text", File.getDefaultDir + "Settings.txt");
			run("Close");			
		}

//Close all windows dialog
	Dialog.create("End of analysis");
	Dialog.addCheckbox("Close all windows?", false);
	Dialog.show();

	close_all = Dialog.getCheckbox();
		if (close_all == true) {
			close("*");
		}

//END
}